buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.2.RELEASE")
        classpath 'com.h2database:h2:1.4.191'
    }
}

plugins { // TODO unify appraoch with below entries
    id "org.flywaydb.flyway" version "5.1.1"
    id 'de.aaschmid.cpd' version '1.0'
}

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: "jacoco"
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'pmd'

version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// keep alphabetical order in groups
dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'

    compile 'com.h2database:h2:1.0.60'
    compile 'io.rest-assured:spring-mock-mvc:3.1.0'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'org.postgresql:postgresql:42.1.4'
    compile "org.projectlombok:lombok:1.18.0"
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-test'
    compile 'org.springframework.boot:spring-boot-starter-web'
}

checkstyle {
    configFile file('checkstyle-config/checkstyle.xml')
    toolVersion '8.11'
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

flyway {
    url = 'jdbc:h2:file:./target/flyway'
    user = 'sa'
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.55 // TODO increase limit
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.80
            }
        }
        rule {
            enabled = false
            element = 'CLASS'
            excludes = ['com.pfm.Application']
            limit {
                counter = 'BRANCH'
                minimum = 0.80
            }
        }
        rule {
            enabled = false
            element = 'CLASS'
            excludes = ['com.pfm.Application']
            limit {
                counter = 'LINE'
                minimum = 0.90
            }
        }
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

test {
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}
