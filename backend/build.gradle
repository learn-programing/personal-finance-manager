buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.4.RELEASE'
        classpath 'org.owasp:dependency-check-gradle:3.3.0'
    }
}

// keep alphabetical order in groups
plugins {
    // 3rd party
    id "com.github.ben-manes.versions" version "0.20.0"
    id "com.gorylenko.gradle-git-properties" version "1.5.1"
    id 'de.aaschmid.cpd' version '1.1'
    id 'io.spring.dependency-management' version "1.0.6.RELEASE"
    id 'org.owasp.dependencycheck' version '3.3.0'
    id 'org.springframework.boot' version '2.0.4.RELEASE'

    // core
    id 'checkstyle'
    id 'findbugs'
    id 'idea'
    id "jacoco"
    id 'java'
    id 'pmd'
    id 'project-report'
}

version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// keep alphabetical order in groups
dependencies {
    testCompile 'com.vmlens:concurrent-junit:1.0.2'
    testCompile 'io.rest-assured:rest-assured:3.1.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.0-M1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.0-M1';
    testCompile 'org.mockito:mockito-core:2.21.0'
    testCompile 'pl.pojo:pojo-tester:0.7.6'
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'
   // testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.0-beta.5'
  //  testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.4'

    compile 'com.h2database:h2:1.4.197'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile "org.flywaydb:flyway-core:5.1.4"
    compile 'org.postgresql:postgresql:42.2.5'
    compile 'org.projectlombok:lombok:1.16.0'
//    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-test'
    compile 'org.springframework.boot:spring-boot-starter-web'
}

checkstyle {
    configFile file('config/checkstyle.xml')
    toolVersion '8.11'
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.96
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.98
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'METHOD'
                minimum = 1.00
            }
        }
        rule {
            element = 'CLASS'
            excludes = ['com.pfm.filters.LoggingFilter'] // TODO cover with tests
            limit {
                counter = 'BRANCH'
                minimum = 1.00
            }
        }
        rule {
            element = 'CLASS'
            excludes = ['com.pfm.filters.LoggingFilter'] // TODO cover with tests
            limit {
                counter = 'LINE'
                minimum = 1.00
            }
        }
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Werror"
}

test {
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

cpdCheck {
    source = sourceSets.main.allJava // only java, groovy and scala classes in 'main' sourceSets
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}
