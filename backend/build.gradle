import com.github.spotbugs.SpotBugsTask

buildscript {
    ext {
        springBootVersion = '2.1.0.RC1'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.owasp:dependency-check-gradle:3.3.2'
    }
}

// keep alphabetical order in groups
plugins {
    // 3rd party
    id "com.github.ben-manes.versions" version "0.20.0"
    id "com.github.spotbugs" version "1.6.5"
    id "com.gorylenko.gradle-git-properties" version "1.5.2"
    id 'de.aaschmid.cpd' version '1.1'
    id 'io.spring.dependency-management' version "1.0.6.RELEASE"
    //TODO dependencyUpdates -Drevision=release -DoutputDir=build/reports/dependencyUpdates .Jenkins
    id 'org.owasp.dependencycheck' version '3.3.2'

    // core
    id 'checkstyle'
    id 'idea'
    id "jacoco"
    id 'java'
    id 'pmd'
    id 'project-report'
}

// ENHANCEMENT not working when moved to block above (will work when moved to official version)
apply plugin: 'org.springframework.boot'

version '1.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

// keep alphabetical order in groups
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testCompile 'com.google.code.findbugs:annotations:3.0.1'
    testCompile 'io.rest-assured:rest-assured:3.1.1'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-junit-jupiter:2.23.0'
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit' // excluding JUnit4
        exclude group: 'org.assertj' // to avoid confustion in imports (e.g. assertThat)
    }

    compile 'com.h2database:h2:1.4.197'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile "org.flywaydb:flyway-core:5.2.0"
    compile 'org.postgresql:postgresql:42.2.5'
    compile 'org.projectlombok:lombok:1.18.2'
    compile 'org.springframework.security:spring-security-crypto'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-web'
}

jacoco {
    toolVersion = "0.8.2"
}

spotbugs {
    toolVersion = '3.1.8'
}

checkstyle {
    configFile file('config/checkstyle.xml')
    toolVersion '8.11'
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'METHOD'
                minimum = 1.00
            }
        }
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Werror"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

cpdCheck {
    source = sourceSets.main.allJava // only java, groovy and scala classes in 'main' sourceSets
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}