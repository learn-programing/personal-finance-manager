import com.github.spotbugs.SpotBugsTask

buildscript {
    ext {
        springBootVersion = '2.2.5.RELEASE'
        lombokVersion = '1.18.12'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.owasp:dependency-check-gradle:4.0.2'
    }
}

// keep alphabetical order in groups
plugins {
    // 3rd party
    id "com.github.ben-manes.versions" version "0.28.0"
    id "com.github.spotbugs" version "3.0.0"
    id "com.gorylenko.gradle-git-properties" version "2.2.2"
    id 'de.aaschmid.cpd' version '3.1'
    id 'io.spring.dependency-management' version "1.0.9.RELEASE"
    id 'org.owasp.dependencycheck' version '5.3.0'
    id 'org.springframework.boot' version '2.2.5.RELEASE'

    // core
    id 'checkstyle'
    id 'idea'
    id "jacoco"
    id 'java'
//  id 'pmd' plugin plugin show million errors after gradle upgrade maybe was not used before ???
    id 'project-report'
}

version '1.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

// keep alphabetical order in groups
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

    testImplementation 'io.rest-assured:rest-assured:3.3.0'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.0'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit' // excluding JUnit4
        exclude group: 'org.assertj' // to avoid confusion in imports (e.g. assertThat)
    }

    implementation 'com.google.code.findbugs:annotations:3.0.1'
    implementation 'com.h2database:h2:1.4.200'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation "org.flywaydb:flyway-core:6.2.4"
    implementation 'org.postgresql:postgresql:42.2.10'
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //Lombok
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
}

jacoco {
    toolVersion = "0.8.5"
}

spotbugs {
    toolVersion = '4.0.0'
}

checkstyle {
    configFile file('config/checkstyle.xml')
    toolVersion '8.11'
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'METHOD'
                minimum = 1.00
            }
        }
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Werror"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

cpdCheck {
    source = sourceSets.main.allJava // only java, groovy and scala classes in 'main' sourceSets
    minimumTokenCount = 70
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}
